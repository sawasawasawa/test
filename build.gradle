ext {
    minSdkVersion = 9
    targetSdkVersion = 25
    compileSdkVersion = 25
    buildToolsVersion = '25.0.2'
    sourceCompatibilityVersion = JavaVersion.VERSION_1_7
    targetCompatibilityVersion = JavaVersion.VERSION_1_7
}

ext.androidToolsVersion = '25.2.0'
ext.supportLibraryVersion = '25.1.0'
ext.kotlinVersion = '1.1.0'

subprojects { project ->
    group = GROUP
    version = VERSION_NAME

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    apply plugin: 'checkstyle'

    task checkstyle(type: Checkstyle) {
        configFile rootProject.file('checkstyle.xml')
        source 'src/main/java'
        ignoreFailures false
        showViolations true
        include '**/*.java'

        classpath = files()
    }

    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('checkstyle')
        }
    }

    buildscript {
        repositories {
            mavenCentral()
            maven { url "https://plugins.gradle.org/m2/" }
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:2.3.3'
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        }
    }
}

ext.deps = [
        kotlin            : "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
        kotlin_reflect    : "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion",
        android           : 'com.google.android:android:4.1.1.4',
        supportAppcompatv7: "com.android.support:appcompat-v7:$supportLibraryVersion",
        supportDesign     : "com.android.support:design:$supportLibraryVersion",
        supportCompat     : "com.android.support:support-compat:$supportLibraryVersion",
        supportAnnotations: "com.android.support:support-annotations:$supportLibraryVersion",
        supportTestRunner : 'com.android.support.test:runner:0.5',
        androidGradle     : 'com.android.tools.build:gradle:2.2.0',
        lint              : "com.android.tools.lint:lint:$androidToolsVersion",
        lintApi           : "com.android.tools.lint:lint-api:$androidToolsVersion",
        lintChecks        : "com.android.tools.lint:lint-checks:$androidToolsVersion",
        lintTests         : "com.android.tools.lint:lint-tests:$androidToolsVersion",
        javapoet          : 'com.squareup:javapoet:1.7.0',
        javaparser        : 'com.github.javaparser:javaparser-core:2.4.0',
        jodaTime          : 'joda-time:joda-time:2.9.3',
        parceler          : 'org.parceler:parceler-api:1.1.8',
        parceler_processor: 'org.parceler:parceler:1.1.8',

        // Test
        junit             : 'junit:junit:4.12',
        kotlin_test       : "org.jetbrains.kotlin:kotlin-test:$kotlinVersion",
        kotlin_junit      : "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion",
        truth             : 'com.google.truth:truth:0.28',
        robolectric       : 'org.robolectric:robolectric:3.1.2',
        openglApi         : 'org.khronos:opengl-api:gl1.1-android-2.1_r1',
        compiletesting    : 'com.google.testing.compile:compile-testing:0.9',
        autoservice       : 'com.google.auto.service:auto-service:1.0-rc2',
        autocommon        : 'com.google.auto:auto-common:0.6',
        uiAutomator       : 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
]